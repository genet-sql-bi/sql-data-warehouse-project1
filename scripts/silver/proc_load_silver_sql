/*
Stored Procedure: Load Silver Layer (Bronze â†’ Silver)

Purpose:
This stored procedure is used to load data from the Bronze layer into the Silver layer.
It applies basic transformations and data cleansing to prepare the data for analytics.

What this procedure does:
- Clears existing data from Silver tables.
- Inserts cleaned and transformed data from Bronze tables into Silver tables.

Parameters:
None.
This procedure does not take any parameters and does not return any values.

How to run:
EXEC silver.load_silver;
*/

CREATE OR ALTER PROCEDURE silver.load_silver AS
BEGIN 
	DECLARE @start_time DATETIME, @end_time DATETIME, @batch_start_time DATETIME, @batch_end_time DATETIME;
	BEGIN TRY
		SET @batch_start_time = GETDATE();
		PRINT '==========================================================================================='
		PRINT'Loading silver Layer'
		PRINT '==========================================================================================='

		PRINT '-------------------------------------------------------------------------------------------'
		PRINT 'Loading CRM Tables'
		PRINT '-------------------------------------------------------------------------------------------'

		--Loading silver.crm_cust_info
		SET @start_time = GETDATE();
		PRINT '>>Truncating Table: silver.crm_cust_info;'
		TRUNCATE TABLE [silver].[crm_cust_info];
		PRINT '>>Inserting Data Into: silver.crm_cust_info';
		INSERT INTO [silver].[crm_cust_info] (
			[cst_id], 
			[cst_key], 
			[cst_firstname], 
			[cst_lastname], 
			[cst_marital_status], 
			[cst_gndr], 
			[cst_create_date]
			)
		SELECT
			[cst_id], 
			[cst_key], 
			TRIM([cst_firstname]) AS cst_firstname, 
			TRIM([cst_lastname]) AS cst_lastname, 
			CASE WHEN UPPER(TRIM(cst_marital_status)) ='S' THEN 'Single'
				WHEN UPPER(TRIM(cst_marital_status)) ='M' THEN 'Married'
				ELSE 'n/a'
				END cst_marital_status, --normalize marital status values to readable format
			CASE WHEN UPPER(TRIM(cst_gndr)) ='F' THEN 'Female'
				WHEN UPPER(TRIM(cst_gndr)) ='M' THEN 'Male'
				ELSE 'n/a' --handling missing data by adding default value
			END cst_gndr, --normalize gender values to readable format
			[cst_create_date]

		FROM (
		SELECT  *,
			ROW_NUMBER() OVER(PARTITION BY cst_id ORDER BY cst_create_date DESC) AS flag_last ---removes duplicates
			FROM [bronze].[crm_cust_info] 
			WHERE cst_id IS NOT NULL) A

			WHERE flag_last = 1 --select the most recent record per customer;
			SET @end_time = GETDATE();
				PRINT '>> Load Duration:' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + ' seconds';
				PRINT '--------------'

		SET @start_time = GETDATE();
		PRINT '>>Truncating Table: silver.crm_prd_info;'
		TRUNCATE TABLE [silver].[crm_prd_info];
		PRINT '>>Inserting Data Into: silver.crm_prd_info';

		INSERT INTO [silver].[crm_prd_info]
			([prd_id], 
			[cat_id], 
			[prd_key], 
			[prd_nm], 
			[prd_cost], 
			[prd_line], 
			[prd_start_dt], 
			[prd_end_dt]
			)
		SELECT 
			[prd_id], 
			REPLACE(SUBSTRING(prd_key, 1, 5), '-', '_') AS cat_id,
			SUBSTRING(prd_key, 7, LEN(prd_key)) AS prd_key,
			[prd_nm], 
			ISNULL(prd_cost, 0) AS prd_cost,
			CASE UPPER(TRIM(prd_line))
				WHEN 'M' THEN 'Mountain'
				WHEN 'R' THEN 'Road'
				WHEN 'S' THEN 'Other Sales'
				WHEN 'T' THEN 'Touring'
				ELSE 'n/a'
			END AS prd_line,
			CAST([prd_start_dt] AS DATE) AS prd_start_dt,
			CAST(LEAD(prd_start_dt) OVER(PARTITION BY prd_key ORDER BY prd_start_dt)- 1 AS DATE) AS prd_end_dt
			FROM [bronze].[crm_prd_info];

		SET @end_time = GETDATE();
			PRINT '>> Load Duration:' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + ' seconds';
			PRINT '--------------' 


		SET @start_time = GETDATE();
		PRINT '>>Truncating Table:silver.crm_sales_details;'
		TRUNCATE TABLE silver.crm_sales_details;
		PRINT '>>Inserting Data Into: silver.crm_sales_details';
		INSERT INTO silver.crm_sales_details(
			[sls_ord_num], 
			[sls_prd_key], 
			[sls_cust_id], 
			[sls_order_dt], 
			[sls_ship_dt], 
			[sls_due_dt], 
			[sls_sales], 
			[sls_quantity], 
			[sls_price])

		SELECT 
			[sls_ord_num], 
			[sls_prd_key], 
			[sls_cust_id], 
			CASE WHEN sls_order_dt = 0 OR LEN(sls_order_dt) != 8 THEN NULL
			ELSE CAST(CAST(sls_order_dt AS VARCHAR) AS DATE)
			END sls_order_dt,
			CASE WHEN sls_ship_dt = 0 OR LEN(sls_ship_dt) != 8 THEN NULL
			ELSE CAST(CAST(sls_ship_dt AS VARCHAR) AS DATE)
			END sls_ship_dt, 
			CASE WHEN sls_due_dt = 0 OR LEN(sls_due_dt) != 8 THEN NULL
			ELSE CAST(CAST(sls_due_dt AS VARCHAR) AS DATE)
			END sls_due_dt, 
			CASE WHEN sls_sales IS NULL OR sls_sales <= 0 OR sls_sales != sls_quantity * ABS(sls_price)
			THEN sls_quantity * ABS(sls_price)
			ELSE sls_sales
		END AS sls_sales,
			[sls_quantity], 
			CASE WHEN sls_price  IS NULL OR sls_price <= 0 
			THEN sls_sales/ NULLIF(sls_quantity, 0)
			ELSE sls_price
			END sls_price
		FROM [bronze].[crm_sales_details];

		SET @end_time = GETDATE();
			PRINT '>> Load Duration:' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + ' seconds';
			PRINT '--------------'
	
	
			PRINT '-------------------------------------------------------------------------------------------'
			PRINT 'Loading ERP Tables'
			PRINT '-------------------------------------------------------------------------------------------'

		SET @start_time = GETDATE();
		PRINT '>>Truncating Table:[silver].[erp_CUST_AZ12];'
		TRUNCATE TABLE [silver].[erp_CUST_AZ12];
		PRINT '>>Inserting Data Into: [silver].[erp_CUST_AZ12]';

		INSERT INTO [silver].[erp_CUST_AZ12] (
		[CID],
		[BDATE],
		[GEN])
		SELECT 
			CASE WHEN CID LIKE 'NAS%' THEN SUBSTRING(CID, 4, LEN(CID))
			ELSE CID
			END CID,
			CASE WHEN BDATE > GETDATE() THEN NULL
			ELSE BDATE
			END BDATE, 
			CASE WHEN UPPER(TRIM(GEN)) IN('F', 'FEMALE') THEN 'Female'
				WHEN UPPER(TRIM(GEN)) IN('M', 'MALE') THEN 'Male'
				ELSE 'n/a'
				END GEN
		FROM [bronze].[erp_CUST_AZ12];
		SET @end_time = GETDATE();
			PRINT '>> Load Duration:' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + ' seconds';
			PRINT '--------------'


		SET @start_time = GETDATE();
		PRINT '>>Truncating Table:[silver].[erp_LOC_A101];'
		TRUNCATE TABLE [silver].[erp_LOC_A101];
		PRINT '>>Inserting Data Into: [silver].[erp_LOC_A101]';

		INSERT INTO [silver].[erp_LOC_A101](
		CID,
		CNTRY)

		SELECT 
			REPLACE(CID, '-', '') AS CID,
		CASE WHEN TRIM(CNTRY) ='DE' THEN 'Germany'
			 WHEN TRIM(CNTRY) IN ('US', 'USA') THEN 'United State'
			 WHEN TRIM(CNTRY) = '' OR CNTRY IS NULL THEN 'n/a'
			 ELSE TRIM (CNTRY)
		END AS CNTRY

		FROM [bronze].[erp_LOC_A101];

		SET @end_time = GETDATE();
			PRINT '>> Load Duration:' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + ' seconds';
			PRINT '--------------'

		SET @start_time = GETDATE();
		PRINT '>>Truncating Table: [silver].[erp_PX_CAT_G1V2] ;'
		TRUNCATE TABLE  [silver].[erp_PX_CAT_G1V2] ;
		PRINT '>>Inserting Data Into:  [silver].[erp_PX_CAT_G1V2] ';

		INSERT INTO [silver].[erp_PX_CAT_G1V2] ([ID], 
			[CAT], 
			[SUBCAT], 
			[MAINTENANCE])
		SELECT 
			[ID], 
			[CAT], 
			[SUBCAT], 
			[MAINTENANCE]
		FROM [bronze].[erp_PX_CAT_G1V2];

	SET @end_time = GETDATE();
			PRINT '>> Load Duration:' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + ' seconds';
			PRINT '--------------'
			SET @batch_end_time = GETDATE();
		PRINT '=================================================='
		PRINT 'Loading Bronze Layer is Completed';
		PRINT '   - Total Load Duration: ' + CAST(DATEDIFF(SECOND, @batch_start_time, @batch_end_time) AS NVARCHAR) + ' seconds';
	END TRY
	BEGIN CATCH
		PRINT '====================================================================================';
		PRINT 'ERROR OCCURED DURING LOADING silver LAYER'
		PRINT 'Error Message' + ERROR_MESSAGE ();
		PRINT 'Error Message' + CAST(ERROR_NUMBER() AS NVARCHAR);
		PRINT 'Error Message' + CAST(ERROR_STATE() AS NVARCHAR);
		PRINT '====================================================================================';
	END CATCH
END
GO

